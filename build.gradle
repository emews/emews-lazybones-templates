buildscript {
    repositories {
        maven {
            url "http://dl.bintray.com/pledbrook/plugins"
        }
        jcenter()
    }

    dependencies {
        classpath "uk.co.cacoethes:lazybones-gradle:1.2.4"
        classpath 'de.undercouch:gradle-download-task:3.3.0'
    }
}

apply plugin: "uk.co.cacoethes.lazybones-templates"
apply plugin: 'de.undercouch.download'

task eqpyPre << {
  copy {
     from 'common/model.sh.gtpl'
     into 'templates/subtmpl-eqpy'
  }

  def eqpy_files = ['EQPy.swift', 'eqpy.py']

  delete {
    eqpy_files.collect { 'templates/subtmpl-eqpy/' + it}
  }

  download {
    src(eqpy_files.collect{
      'https://raw.githubusercontent.com/emews/EQ-Py/master/src/' + it
    })
    dest 'templates/subtmpl-eqpy'
  }
}

task eqrPre << {
  copy {
     from 'common/model.sh.gtpl'
     into 'templates/subtmpl-eqr'
  }

  def eqr_files = ['BlockingQueue.h',
    'EQR.cpp',
    'EQR.h',
    'EQR.i',
    'EQR.swift',
    'Makefile.in',
    'bootstrap',
    'configure.ac',
    'make-package.tcl',
    'settings.mk.in',
    'settings.template.sh']

  delete {
    eqr_files.collect { 'templates/subtmpl-eqr/eqr/' + it}
  }

  download {
    src(eqr_files.collect{
      'https://raw.githubusercontent.com/emews/EQ-R/master/src/' + it
    })
    dest 'templates/subtmpl-eqr/eqr/'
  }

  exec {
    commandLine 'chmod', 'ug+x', 'templates/subtmpl-eqr/eqr/bootstrap'
  }
}

task sweepPre << {
  copy {
     from 'common/model.sh.gtpl'
     into 'templates/subtmpl-sweep'
  }
}

packageTemplateSubtmplEqpy.dependsOn eqpyPre
packageTemplateSubtmplEqr.dependsOn eqrPre
packageTemplateSubtmplSweep.dependsOn sweepPre

lazybones {
    repositoryName = "emews/emews-templates"

    // It's best to pull this information from a gradle.properties file in the
    // root directory. For example, gradle.properties could contain:
    //
    //     bintrayUsername=dilbert
    //     bintrayApiKey=kdshfiu2htr082hg0h2ghkhsdafkh
    //
    // You could then set the following properties to `project.bintrayUsername`
    // and `project.bintrayApiKey` respectively.
    repositoryUsername = project.bintrayUsername
    repositoryApiKey = project.bintrayApiKey

    // These properties are required for Bintray open source hosting. Uncomment
    // and substitute in your project's license and vcsUrl:

    licenses = ["BSD New"]
    vcsUrl = "https://github.com/emews/emews-lazybones-templates"

    template "emews" includes "sweep", "eqpy", "eqr"
}
